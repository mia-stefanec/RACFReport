 INITSQL: PROCEDURE OPTIONS(MAIN);
 DCL DATA_LEN FIXED BIN(31) VALUE(80);
 DCL DATA_DIM FIXED BIN(31) VALUE(20);
 DCL 1 ERROR_MESSAGE AUTOMATIC,
      3 ERROR_LEN    FIXED BIN(15) UNAL INIT((DATA_LEN*DATA_DIM)),
      3 ERROR_TEXT(DATA_DIM) CHAR(DATA_LEN);
 DCL DSNTIAR ENTRY OPTIONS (ASM,INTER,RETCODE);

 PUT EDIT('NAZIV TVRTKE','KORISNIK','ADRESA TVRTKE')(A,COL(64),A,
                                                     SKIP(1),A);
 PUT SKIP EDIT((72)'-')(A);
 PUT SKIP EDIT('IZVJESCE O NEUSPJESNIM POKUSAJIMA PRISTUPA')(X(16),A);
 PUT SKIP EDIT('SUSTAVU','HZMO',(72)'-')(X(33),A,SKIP(1),
                                                    X(34),A,SKIP(1),A);

 EXEC SQL INCLUDE SQLCA;
 EXEC SQL INCLUDE VJOBINIT;
 EXEC SQL

   DECLARE JOBINITC CURSOR FOR
   SELECT  EVENT_QUAL,
   EVT_USER_ID,
   TIME_WRITTEN,
   DATE_WRITTEN,
   USER_NAME,
   SYSTEM_SMFID,
   TERM
   FROM VJOBINIT
   WHERE (TIMESTAMP_WRITTEN >= CURRENT TIMESTAMP - 1 DAY)
         AND (EVENT_QUAL NOT IN ('SUCCESS','TERM','SUCCESSI',
           'SUCCESST','RACINITI','RACINITD','SUCCESSP','SUCCESSM',
           'MFAPSUCC'));

 EXEC SQL OPEN JOBINITC;

 IF (SQLCODE < 0) THEN DO;
   CALL DSNTIAR (SQLCA, ERROR_MESSAGE, DATA_LEN);
   PUT SKIP DATA(ERROR_MESSAGE.ERROR_TEXT);
   SIGNAL ERROR;
 END;

 IF (SQLCODE = 100) THEN
   EXEC SQL CLOSE JOBINITC;

 DO WHILE (SQLCODE=0);
  REINIT VJOBINIT;
  EXEC SQL
    FETCH JOBINITC INTO :VJOBINIT.EVENT_QUAL,
    :VJOBINIT.EVT_USER_ID,
    :VJOBINIT.TIME_WRITTEN,
    :VJOBINIT.DATE_WRITTEN,
    :VJOBINIT.USER_NAME,
    :VJOBINIT.SYSTEM_SMFID,
    :VJOBINIT. TERM;
  IF SQLCODE <> 0 THEN LEAVE;

  PUT SKIP EDIT('DO:',VJOBINIT.EVENT_QUAL,
  'ID:',VJOBINIT.EVT_USER_ID,
  'V:',VJOBINIT.TIME_WRITTEN,
  'DM:',VJOBINIT.DATE_WRITTEN,
  'S:',VJOBINIT.SYSTEM_SMFID)(A,COL(5),A,COL(15),A,COL(19),A,
  COL(29),A,COL(32),A,COL(42),A,COL(46),A,COL(58),A,COL(61),A);

  PUT SKIP EDIT('T:',VJOBINIT.TERM,
   'IK:',VJOBINIT.USER_NAME)(A,COL(4),A,COL(15),A,
   COL(19),A);

  PUT SKIP EDIT('#',(70)'-','#')(A);
 END;

 PUT SKIP;
 PUT SKIP EDIT('DO DOGADAJ, ID KORISNIK, V VRIJEME, DM DATUM,S SUSTAV,',
 'T TERMINAL, IK IME KORISNIKA')
 (A,SKIP,A);

 PUT SKIP EDIT((72)'*')(A);

 PUT SKIP EDIT('LEGENDA DOGADAJA:')(A);

 PUT SKIP EDIT('INVPSWD','NETOCNA LOZINKA','PWDEXPR',
 'LOZINKA JE ISTEKLA','INVNPWD','NEVAZECA NOVA LOZINKA')
 (A,COL(11),A,SKIP);

 PUT SKIP EDIT('INVPHRS','NETOCNA FRAZA(LOZINKA)','INVNPHRS',
 'NEVAZECA NOVA FRAZA','PHRSEXPR','FRAZA JE ISTEKLA')(A,COL(11),A,SKIP);

 PUT SKIP EDIT('REVKAUTO','PONISTEN PROFIL(VISESTRUKE NETOCNE PRIJAVE)',
 'REVKINAC','KORISNIKU PONISTEN PROFIL(NEAKT.)')(A,COL(11),A,SKIP);

 PUT SKIP EDIT('REVKUSER','KORISNIKU JE PONISTEN PROFIL','INVTERM',
 'NEVAZECI TERMINAL','INVGRP','NEVAZECA GRUPA')(A,COL(11),A,SKIP);

 PUT SKIP EDIT('UNDFUSER','KORISNIK NIJE DEFINIRAN U RACF-U','USERNJOB',
 'KORISNIK NIJE AUTORIZIRAN ZA JOB','INVAPPL','NEAUTOR. PRISTUP APLIK.',
 'GRPARVKD','GRUPI JE PONISTEN PRISTUP')(A,COL(11),A,SKIP);

 END INITSQL;
